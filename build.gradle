plugins {
  id "java-library"
  id "maven"
  id 'net.researchgate.release' version '2.8.1'
}

apply from: "nexus.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenLocal()
  jcenter()
}

dependencies {
  compileOnly 'org.projectlombok:lombok:1.18.12'
  annotationProcessor 'org.projectlombok:lombok:1.18.12'
  api 'net.jodah:failsafe:2.3.5'
  api 'org.apache.kafka:kafka-clients:2.5.0'

  testCompileOnly 'org.projectlombok:lombok:1.18.12'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'

  testRuntimeOnly('ch.qos.logback:logback-classic:1.2.3')
  testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
  testImplementation "org.mockito:mockito-junit-jupiter:3.3.3"
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

compileTestJava {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

release {
  project.ext.set("release.useAutomaticVersion", true)
  git {
    requireBranch = ''
  }
  failOnCommitNeeded = false
  failOnPublishNeeded = false
  failOnUnversionedFiles = false
  buildTasks = []
}

confirmReleaseVersion.doLast {
  def f = file("${project.projectDir}/README.md")
  f.text = f.text.replaceAll("(:\\ )*\\d+\\.\\d+\\.\\d+", "\$1${version}")
}
